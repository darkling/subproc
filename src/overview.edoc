@author Stanislaw Klekot <dozzie@jarowit.net>
@title Unix subprocess manager for Erlang
@version 0.1.0
@see subproc
@see subproc_unix
@doc

`subproc' is a unix subprocess manager for Erlang. It works much closer with
operating systems than Erlang, so over built-in ports it provides several
advantages:

<ul>
  <li>tracks subprocess' PID</li>
  <li>can spawn a subprocess in a process group</li>
  <li>can send a signal to a subprocess or its process group</li>
  <li>subprocess' <i>STDIO</i> can be redirected with `pipe(2)' or
      `socketpair(2)'</li>
  <li>spawned port can work in `{active,once}' and passive modes known from
      {@link gen_tcp} and other socket modules, which provides a sensible
      backpressure to external command (running `yes(1)' won't trigger OOM
      killer)</li>
</ul>

`subproc' can also spawn ports from already-opened descriptors, which is
mainly intended for reading from <i>STDIN</i> with backpressure.

== Examples ==

Active mode, data returned as strings:

```
1> application:start(subproc).
ok
2> {ok, Port} = subproc:exec("/bin/ls", ["-l", "/etc/pam.d"],
                             [{stdio, in}, {packet, line}, {active, true}]).
{ok,#Port<0.656>}
3> flush().
Shell got {subproc,#Port<0.656>,"total 84\n"}
Shell got {subproc,#Port<0.656>,
                   "-rw-r--r-- 1 root root  384 Nov 22  2008 chfn\n"}
Shell got {subproc,#Port<0.656>,
                   "-rw-r--r-- 1 root root   92 Feb 15  2011 chpasswd\n"}
Shell got {subproc,#Port<0.656>,
                   "-rw-r--r-- 1 root root  581 Nov 22  2008 chsh\n"}
Shell got {subproc,#Port<0.656>,
                   "-rw-r--r-- 1 root root 1208 Mar 28  2015 common-account\n"}
% ...
Shell got {subproc,#Port<0.656>,
                   "-rw-r--r-- 1 root root   95 Feb 16  2015 sudo\n"}
Shell got {subproc_closed,#Port<0.656>}
ok
4> subproc:setopts(Port, [{exit_status, true}]).
ok
5> flush().
Shell got {subproc_terminated,#Port<0.656>,exit,0}
ok
6> subproc:status(Port).
{exit,0}
'''

Passive mode, data returned as binaries:

```
1> application:start(subproc).
ok
2> {ok, Port} = subproc:exec("/bin/ls", ["-l", "/etc/pam.d"],
                             [{stdio, in}, {packet, line}, binary]).
{ok,#Port<0.656>}
3> subproc:recv(Port, 0).
{ok,<<"total 84\n">>}
4> subproc:recv(Port, 0).
{ok,<<"-rw-r--r-- 1 root root  384 Nov 22  2008 chfn\n">>}
5> subproc:recv(Port, 0).
{ok,<<"-rw-r--r-- 1 root root   92 Feb 15  2011 chpasswd\n">>}
% ...
17> subproc:recv(Port, 0).
{ok,<<"-rw-r--r-- 1 root root   95 Feb 16  2015 sudo\n">>}
18> subproc:recv(Port, 0).
eof
19> subproc:status(Port).
{exit,0}
20> subproc:setopts(Port, [{exit_status, true}]).
ok
21> flush().
Shell got {subproc_terminated,#Port<0.656>,exit,0}
ok
'''

Reading from STDIN for first 5 seconds:

```
#!/usr/bin/escript
%%! -noinput

%% NOTE: the `-noinput' option above is important, it prevents Erlang from
%% spawning a port that uses descriptor 0

main(_) ->
  ok = application:start(subproc),
  {ok, STDIN} = subproc:open({in, 0}, [{packet, line}, {active, once}]),
  erlang:send_after(timer:seconds(5), self(), timeout),
  read_all(STDIN).

read_all(Port) ->
  receive
    timeout ->
      io:fwrite("timeout, exiting~n");
    {subproc, Port, Line} ->
      io:fwrite("line: [~s]~n", [string:strip(Line, right, $\n)]),
      subproc:setopts(Port, [{active, once}]),
      read_all(Port);
    {subproc_closed, Port} ->
      io:fwrite("EOF, exiting~n");
    {subproc_error, Port, Reason} ->
      io:fwrite("read error: ~p~n", [Reason])
  end.
'''

== Architecture ==

To implement communication modes known from socket modules (`{active,false}'
and `{active,once}'), `subproc' uses custom port driver, `subproc_drv'.

Subprocesses are executed from a separate OS process, which is started as an
executable `subproc_unix_supervisor'. Separate OS process enables `subproc' to
clean up even in the face of crash of whole Erlang VM, while using an
executable displays a meaningful process name in process list (`ps(1)') and
allows to use SUID/SGID permissions to spawn processes as different users than
Erlang runs as.

To execute `subproc_unix_supervisor' and communicate with it, another port
driver called `subproc_sup_drv' is employed. This port driver has only one
instance, owned by a process in `subproc' supervision tree.

Mapping between spawned subprocesses and ports is remembered in `subproc'
supervision tree as well. This allows user to send unix signals even to
subprocesses with a native port as a communication handle.

Each of the port drivers has its own low-level API module that wraps
`erlang:port_*()' function calls.

A limited number of unix API functions is provided {@link subproc_unix}
module, which is implemented as a NIF library. This module also provides few
translation functions between names and numbers (`errno(3)' and signals).

=== Supervision tree ===

There are three worker processes in the supervision tree.

`subproc_master' is the central process for the application. It opens
`subproc_sup_drv' port and orders it to execute `subproc_unix_supervisor'.
It also translates {@link subproc:exec/3} arguments to requests to
`subproc_unix_supervisor' and execution results back to returned values,
forwards OS process termination events to `subproc'-type ports, forwards
signal sending requests to `subproc_unix_supervisor', and closes file
descriptors after native ports terminate.

`subproc_mdrv_reaper' monitors the port (`subproc_sup_drv') owned by
`subproc_master' and cleans up the zombie process left after the port is
closed. Since closing port cannot wait for `subproc_unix_supervisor' to
exit (it can take seconds before children exit themselves), it needs to be
done on Erlang's side. Separate Erlang process ensures that a crash of
`subproc_master' doesn't leave zombies.

`subproc_driver_loader' is tasked with loading and unloading port drivers and
NIF library and does nothing beside that.

